apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

def buildTime() {
    return new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))
}

def versionMajor = 1
def versionMinor = 0
def versionPatch = 3
def versionBuild = 19 // bump for dogfood builds, public betas, etc.

android {
    signingConfigs {
        SigningConfig {
            try {
                keyAlias 'TesterHome'
                storeFile file('keystore.jks')
                storePassword KEYSTORE_PASSWORD
                keyPassword KEY_PASSWORD
            } catch (ex) {
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
    }
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    defaultConfig {
        applicationId "com.testerhome.nativeandroid"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.SigningConfig
        }
        debug {
            signingConfig signingConfigs.SigningConfig
        }
    }
    lintOptions {
        abortOnError false
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:customtabs:23.1.1'
    compile 'com.google.android.gms:play-services-analytics:8.3.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'de.greenrobot:greendao:2.0.0'
    compile 'com.facebook.fresco:fresco:0.8.1'
    compile 'com.facebook.fresco:imagepipeline-okhttp:0.8.1'
    compile 'com.umeng.analytics:analytics:5.6.1'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'com.squareup.retrofit:adapter-rxjava:2.0.0-beta2'
    compile('io.reactivex:rxandroid:1.1.0') {
        exclude module: 'rxjava'
    }
    compile 'com.google.code.gson:gson:2.4'
    compile project(':swipebackhelper')
}
